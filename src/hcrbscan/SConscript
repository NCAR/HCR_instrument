# -*- python -*-

import os
import re
import eol_scons

def Bscan(env):
    env['DOXYFILE_DICT'].update({ 'HTML_HEADER' : env.File("#header.html") })
    env['DOXYGEN'] = '/usr/bin/doxygen'
    env['TAGDIR'] = '#/doc/tags'
    env.SetDoxref('QWT_DOXREF','$TAGDIR/qwt-5.2.x.tag',
              'http://qwt.sourceforge.net')
    env.SetDoxref('QT_DOXREF', '$TAGDIR/qt-4.4.3.tag',
                  'http://doc.trolltech.com/4.4')
    return env

tools = Split("""
qtt_qtconfig
qtt_qtbscan
rapradar
doxygen
boost_program_options
""")

env = Environment(tools = ['default'] + tools,
                  GLOBAL_TOOLS = ['svninfo', 'doxygen', Bscan])

# Enable for profiling
#env.Append(CCFLAGS = ['-pg'])
#env.Append(LINKFLAGS = ['-pg'])

# create or possibly update the svnInfo.h file
svnInfo = env.SvnInfo('svnInfo.h', '#')
svnversion = env['SVNREVISION']

bscanSources = Split('''
hcrbscan.cpp
RadxBscanRaySource.cpp
ReaderThread.cpp
''')

bscanHeaders = Split('''
RadxBscanRaySource.h
ReaderThread.h
''')

bscan = env.Program('hcrbscan', bscanSources)
env.InstallProgram(bscan)
Default(bscan)

html = env.Apidocs(bscanSources + bscanHeaders)
Default(html)

options = env.GlobalOptions()
options.Update(env)
Help(options.GenerateHelpText(env))

env.Alias('all', DEFAULT_TARGETS)

