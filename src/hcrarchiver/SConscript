import os

sources = []

tsarchiverSources = Split('''
TsArchiver.cpp
TsArchiverMain.cpp
TimeSeriesAdapter.cpp
''')
sources += tsarchiverSources

tsplaybackSources = Split('''
IwrfTsFilesReader.cpp
TsPlayback.cpp
TsPlaybackMain.cpp
TimeSeriesAdapter.cpp
''')
sources += tsplaybackSources

ts2peifileSources = Split('''
IwrfTsFilesReader.cpp
Ts2PeiFile.cpp
''')
sources += ts2peifileSources

productarchiverSources = Split('''
ProductArchiver.cpp
ProductArchiverMain.cpp
''')
sources += productarchiverSources

headers = Split('''
IwrfTsFilesReader.h
TimeSeriesAdapter.h
TsArchiver.h
TsPlayback.h
ProductArchiver.h
''')

inst = []


tstools = Split("""
boost_program_options
ddssvc
profilerdds
qtt_qtconfig
radd_archiver
rapradar
""")
tsenv = Environment(tools = ['default'] + tstools)

# build tsarchiver
tsarchiver = tsenv.Program('tsarchiver', tsarchiverSources)
inst += tsenv.InstallProgram(tsarchiver)
tsenv.Default(tsarchiver)

# build tsplayback
tsplayback = tsenv.Program('tsplayback', tsplaybackSources)
inst += tsenv.InstallProgram(tsplayback)
tsenv.Default(tsplayback)

# build ts2peifile
ts2peifile = tsenv.Program('ts2peifile', ts2peifileSources)
inst += tsenv.InstallProgram(ts2peifile)
tsenv.Default(ts2peifile)

# documentation
html = tsenv.Apidocs(sources + headers, DOXYFILE_FILE = "Doxyfile")
tsenv.AppendDoxref('tsArchiver')
tsenv.Default(html)




producttools = Split("""
boost_program_options
ddssvc
profilerdds
qtt_qtconfig
rapradx
xmlrpc
""")
productenv = Environment(tools = ['default'] + producttools)

# build productarchiver
productarchiver = productenv.Program('productarchiver', productarchiverSources)
inst += productenv.InstallProgram(productarchiver)
productenv.Default(productarchiver)

productenv.Alias('install', inst)
